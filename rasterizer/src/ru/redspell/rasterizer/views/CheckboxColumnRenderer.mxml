<?xml version="1.0"?>
<s:GridItemRenderer
	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:mx="library://ns.adobe.com/flex/mx">

	<fx:Script><![CDATA[
		import mx.binding.utils.BindingUtils;
		import mx.events.CollectionEvent;
		import mx.events.FlexEvent;
		import mx.events.FlexNativeMenuEvent;

		import ru.redspell.rasterizer.models.SwfsPack;

		import spark.components.DataGrid;

		public static const CHECK_ALL:String = 'checkboxColumnRenderer_checkAll';
		public static const UNCHECK_ALL:String = 'checkboxColumnRenderer_uncheckAll';

		protected function menu_menuShowHandler(event:FlexNativeMenuEvent):void {
			var dp:ArrayCollection = grid.dataGrid.dataProvider as ArrayCollection;
			var menuDp:ArrayCollection = (flexContextMenu as FlexNativeMenu).dataProvider as ArrayCollection;
			var checkAllItem:Object = menuDp.getItemAt(0);

			for each (var item:Object in dp.source) {
				if (!item[column.dataField]) {
					checkAllItem.label = 'Check all';
					checkAllItem.action = CHECK_ALL;
					menuDp.refresh();

					return;
				}
			}

			checkAllItem.label = 'Uncheck all';
			checkAllItem.action = UNCHECK_ALL;
			menuDp.refresh();
		}

		protected function menu_itemClickHandler(event:FlexNativeMenuEvent):void {
			var dp:ArrayCollection = grid.dataGrid.dataProvider as ArrayCollection;
			var dataGrid:DataGrid = grid.dataGrid;
			var chkboxVal:Boolean = event.item.action == CHECK_ALL;

			for each (var item:Object in dp.source) {
				item[column.dataField] = chkboxVal;
			}

			dp.refresh();
		}

		public function set dataFieldValue(value:Boolean):void {
			data[column.dataField] = value;
		}
	]]></fx:Script>

	<fx:Binding source="checked.selected" destination="dataFieldValue"/>

	<s:HGroup width="100%" height="100%" horizontalAlign="center" verticalAlign="middle">
		<s:CheckBox id="checked" selected="{data[column.dataField]}"/>
	</s:HGroup>

	<s:flexContextMenu>
		<mx:FlexNativeMenu labelField="label" menuShow="menu_menuShowHandler(event)" itemClick="menu_itemClickHandler(event)">
			<mx:dataProvider>
				<mx:ArrayCollection>
					<fx:Object/>
				</mx:ArrayCollection>
			</mx:dataProvider>
		</mx:FlexNativeMenu>
	</s:flexContextMenu>
</s:GridItemRenderer>
