<?xml version="1.0"?>
<s:GridItemRenderer
	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:mx="library://ns.adobe.com/flex/mx">

	<fx:Script><![CDATA[
		import mx.binding.utils.BindingUtils;
		import mx.events.CollectionEvent;
		import mx.events.FlexEvent;
		import mx.events.FlexNativeMenuEvent;

		import ru.nazarov.asmvc.command.AbstractMacroCommand;

		import ru.nazarov.asmvc.command.ICommand;
		import ru.nazarov.asmvc.command.IMacroCommand;
		import ru.redspell.rasterizer.commands.SavePackMetaCommand;
		import ru.redspell.rasterizer.models.ClassProfile;
		import ru.redspell.rasterizer.models.Swf;
		import ru.redspell.rasterizer.models.SwfClass;

		import ru.redspell.rasterizer.models.SwfsPack;
		import ru.redspell.rasterizer.views.events.CheckboxColumnRendererEvent;

		import spark.components.DataGrid;
		import spark.components.gridClasses.GridSelectionMode;

		public static const CHECK_ALL:String = 'checkboxColumnRenderer_checkAll';
		public static const UNCHECK_ALL:String = 'checkboxColumnRenderer_uncheckAll';
		public static const CHECK_SELECTION:String = 'checkboxColumnRenderer_checkSelection';
		public static const UNCHECK_SELECTION:String = 'checkboxColumnRenderer_uncheckSelection';

		protected function menu_menuShowHandler(event:FlexNativeMenuEvent):void {
			var dp:ArrayCollection = grid.dataGrid.dataProvider as ArrayCollection;
			var menuDp:ArrayCollection = (flexContextMenu as FlexNativeMenu).dataProvider as ArrayCollection;
			var chkAllItem:Object = menuDp.getItemAt(0);
			var checkAll:Boolean = false;

			for each (var item:Object in dp.source) {
				if (!item[column.dataField]) {
					checkAll = true;
					break;
				}
			}

			if (checkAll) {
				chkAllItem.label = 'Check all';
				chkAllItem.action = CHECK_ALL;
			} else {
				chkAllItem.label = 'Uncheck all';
				chkAllItem.action = UNCHECK_ALL;
			}

			var dataGrd:DataGrid = grid.dataGrid;

			if (dataGrd.selectionMode == GridSelectionMode.MULTIPLE_ROWS) {
				if (menuDp.length < 2) {
					var chkSltnItem:Object = {};
					menuDp.addItem(chkSltnItem);
				} else {
					chkSltnItem = menuDp.getItemAt(1);
				}

				var checkSelection:Boolean = false;

				for each (item in dataGrd.selectedItems) {
					if (!item[column.dataField]) {
						checkSelection = true;
						break;
					}
				}

				if (checkSelection) {
					chkSltnItem.label = 'Check selection';
					chkSltnItem.action = CHECK_SELECTION;
				} else {
					chkSltnItem.label = 'Uncheck selection';
					chkSltnItem.action = UNCHECK_SELECTION;
				}
			} else if (menuDp.length > 1) {
				menuDp.removeItemAt(1);
			}

			menuDp.refresh();
		}

		protected function menu_itemClickHandler(event:FlexNativeMenuEvent):void {
			var dataGrid:DataGrid = grid.dataGrid;
			var dp:ArrayCollection = grid.dataGrid.dataProvider as ArrayCollection;

			if (event.item.action == CHECK_ALL || event.item.action == UNCHECK_ALL) {
				var items:Array = dp.source;
				var chkboxVal:Boolean = event.item.action == CHECK_ALL;
			} else {
				items = [];
				chkboxVal = event.item.action == CHECK_SELECTION;

				for each (var sltnItem:Object in dataGrid.selectedItems) {
					items.push(sltnItem);
				}
			}

			for each (var item:Object in items) {
				item[column.dataField] = chkboxVal;

				if (item is SwfsPack) {
					var command:ICommand = Facade.commandsFactory.getRefreshPackMetaCommand(item as SwfsPack, false);

					if (saveCmd == null) {
						var saveCmd:ICommand = new AbstractMacroCommand();
					}

					(saveCmd as IMacroCommand).addSubcommand(new SavePackMetaCommand(item as SwfsPack));
				} else if (item is Swf) {
					command = Facade.commandsFactory.getRefreshSwfMetaCommand(item as Swf, false);

					if (saveCmd == null) {
						saveCmd = Facade.commandsFactory.getSavePackMetaCommand((item as Swf).pack);
					}

				} else if (item is SwfClass) {
					command = Facade.commandsFactory.getRefreshClassMetaCommand(item as SwfClass, false);

					if (saveCmd == null) {
						saveCmd = Facade.commandsFactory.getSavePackMetaCommand((item as SwfClass).swf.pack);
					}
				} else if (item is ClassProfile) {
					command = Facade.commandsFactory.getRefreshClassMetaCommand((item as ClassProfile).cls as SwfClass, false);
				}

				if (command) {
					Facade.runCommand(command);
				}
			}

			if (saveCmd != null) {
				Facade.runCommand(saveCmd);
			}

			var selection:Vector.<Object> = dataGrid.selectedItems;

			dp.refresh();
			dataGrid.selectedItems = selection;

			dispatchChangedEvent();
		}

		protected function dispatchChangedEvent():void {
			trace('dispatchChangedEvent');
			if (column is CheckboxGridColumn) {
				column.dispatchEvent(new CheckboxColumnRendererEvent(CheckboxColumnRendererEvent.CHANGED, (column.grid.dataProvider as ArrayCollection).getItemAt(rowIndex)));
			}
		}

		public function set dataFieldValue(value:Boolean):void {
			data[column.dataField] = value;
		}
		]]></fx:Script>

	<fx:Binding source="checked.selected" destination="dataFieldValue"/>

	<s:HGroup width="100%" height="100%" horizontalAlign="center" verticalAlign="middle">
		<s:CheckBox id="checked" selected="{data[column.dataField]}" change="dispatchChangedEvent()"/>
	</s:HGroup>

	<s:flexContextMenu>
		<mx:FlexNativeMenu labelField="label" menuShow="menu_menuShowHandler(event)" itemClick="menu_itemClickHandler(event)">
			<mx:dataProvider>
				<mx:ArrayCollection>
					<fx:Object/>
					<fx:Object/>
				</mx:ArrayCollection>
			</mx:dataProvider>
		</mx:FlexNativeMenu>
	</s:flexContextMenu>
</s:GridItemRenderer>
