<?xml version="1.0"?>
<mx:MenuBar
	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:mx="library://ns.adobe.com/flex/mx"
	width="100%"
	itemClick="itemClickHandler(event)"
	dataProvider="{dp}">

	<fx:Script><![CDATA[
		import mx.collections.ArrayCollection;
		import mx.collections.ListCollectionView;
		import mx.collections.XMLListCollection;
		import mx.events.CollectionEvent;
		import mx.events.MenuEvent;
		import ru.redspell.rasterizer.commands.BatchProcessSwfCommand;

		import ru.redspell.rasterizer.models.Profile;

		public static const OPEN:String = 'mainMenu_open';
		public static const EXPORT:String = 'mainMenu_export';
		public static const EXPORT_ALL:String = 'mainMenu_exportAll';
		public static const CREATE_PROFILE:String = 'mainMenu_createProfile';
		public static const CHOOSE_PROFILE:String = 'mainMenu_chooseProfile';
		public static const BATCH_PROCESS1:String = 'mainMenu_batchProcess1';
		public static const BATCH_PROCESS2:String = 'mainMenu_batchProcess2';

		protected var chooseItem:Object = { label:'Choose', children:[] };

		[Bindable]
		protected var dp:Array = [
			{ label:'File', children:[
				{ label:'Open', action:OPEN },
				{ label:'Export', action:EXPORT },
				{ label:'Export All Profiles', action:EXPORT_ALL },
				{ label:'Batch: check folder', action:BATCH_PROCESS1 },
				{ label:'Batch: process folder', action:BATCH_PROCESS2 }
			] },
			{ label:'Profiles', children:[
				{ label:'Create', action:CREATE_PROFILE },
				chooseItem
			] }
		];

		protected function itemClickHandler(event:MenuEvent):void {
			switch (event.item.action) {
				case OPEN:
					Facade.runCommand(Facade.commandsFactory.getOpenProjectCommand());
					break;

				case EXPORT:
					Facade.runCommand(Facade.commandsFactory.getExportCommand(Facade.proj));
					break;

				case EXPORT_ALL:
					Facade.runCommand(Facade.commandsFactory.getExportCommand(Facade.proj, Facade.profiles.source));
					break;

				case CREATE_PROFILE:
					if (Facade.projDir != null) {
						Facade.app.showPopup(new CreateProfileWindow());
					}

					break;

				case CHOOSE_PROFILE:
					Facade.runCommand(Facade.commandsFactory.getChooseProfileCommand(event.item.data as Profile))
					break;
					
				case BATCH_PROCESS1:
					Facade.runCommand(new BatchProcessSwfCommand(false));
					break;
					
				case BATCH_PROCESS2:
					Facade.runCommand(new BatchProcessSwfCommand(true));
					break;
			}
		}

		public function setProfilesDp(dp:ArrayCollection):void {
			dp.addEventListener(CollectionEvent.COLLECTION_CHANGE, profilesDp_collectionChangeHandler);
			dp.refresh();
		}

		protected function profilesDp_collectionChangeHandler(event:CollectionEvent):void {
			chooseItem.children = [];

			for each (var profileModel:Profile in (event.target as ArrayCollection).source) {
				var profile:Object = { label: profileModel.label, action:CHOOSE_PROFILE, data:profileModel, type:'radio', toggled:(profileModel == Facade.profile) };
				chooseItem.children.push(profile);
			}
		}
	]]></fx:Script>
</mx:MenuBar>