<?xml version="1.0"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:mx="library://ns.adobe.com/flex/mx" title="Preview" width="500" height="400" close="closeHandler()" initialize="init()">
	<fx:Script><![CDATA[
		import flash.display.Bitmap;
		import flash.display.BitmapData;
		import flash.display.DisplayObject;
		import mx.managers.PopUpManager;
		import ru.redspell.rasterizer.flatten.FlattenMovieClip;
		import ru.redspell.rasterizer.flatten.IFlatten;
		import spark.filters.GlowFilter;


		protected var _obj:IFlatten;
		protected var _timer:Timer = new Timer(40);

		protected function init():void {
			_timer.addEventListener(TimerEvent.TIMER, timerHandler);
			bg.addChild(new Bitmap(new BitmapData(500, 300, false, 0x0)));
		}

		protected function timerHandler(event:TimerEvent):void {
			var fmc:FlattenMovieClip = _obj as FlattenMovieClip;

			fmc.nextFrame();
			frameLbl.text = 'frame: ' + fmc.curFrame;
		}

		protected function closeHandler():void {
			PopUpManager.removePopUp(this);
			_obj.dispose();
			_timer.stop();
		}

		public function preview(obj:IFlatten):void {
			trace("preview1");

			_obj = obj;
			_obj.render();

			if (_obj is DisplayObject) {
				var dobj:DisplayObject = _obj as DisplayObject;
				container.addChild(dobj);
			} else {
				var bmp:Bitmap = new Bitmap(_obj as BitmapData);
				container.addChild(bmp);
			}

			var rect:Rectangle = container.getBounds(this);
			var fltMc:FlattenMovieClip = _obj as FlattenMovieClip

			//when obj rect is empty, container rect is something like (x=6710887.4, y=6710918.6, w=0, h=0); we need to find non-empty-rect frame to align container correctly
			if (fltMc != null) {
				while (rect.isEmpty() && (fltMc.curFrame < fltMc.frames.length)) {
					fltMc.nextFrame();
					rect = container.getBounds(this);
				}

				fltMc.goto(0);
			}

			if (rect.width > containerGrp.width) {
				container.scaleX = container.scaleY = containerGrp.width / rect.width;
				rect = container.getBounds(this);
			}

			if (rect.height > containerGrp.height) {
				container.scaleX = container.scaleY = containerGrp.height / rect.height * container.scaleX;
				rect = container.getBounds(this);
			}

			rect = container.getRect(containerGrp);

			container.x = (containerGrp.width - rect.width) / 2 - rect.x ;
			container.y = (containerGrp.height - rect.height) / 2 - rect.y;
			//dobj.x = -rect.x;
			//(_obj as DisplayObject).x = -rect.x;
			//(_obj as DisplayObject).y = -rect.y;
			//(_obj as DisplayObject).x = (containerGrp.width  - rect.width - rect.x) / 2;
			//(_obj as DisplayObject).y = (containerGrp.height - rect.height + rect.y) / 2;

			if (fltMc != null) {
				_timer.start();
			}

			trace("preview2");
		}

		protected function switchFrame(next:Boolean = true):void {
			if (_timer.running) {
				playBtn.label = 'play';
				_timer.stop();
			}

			var fmc:FlattenMovieClip = _obj as FlattenMovieClip;

			if (fmc) {
				if (next) {
					fmc.nextFrame();
				} else {
					fmc.prevFrame();
				}

				frameLbl.text = 'frame: ' + fmc.curFrame;
			}
		}

		protected function togglePlay():void {
			var fmc:FlattenMovieClip = _obj as FlattenMovieClip;

			if (_timer.running) {
				playBtn.label = 'play';
				_timer.stop();
			} else {
				playBtn.label = 'stop';
				_timer.start();
			}
		}
		
		private function toggleBg():void {
			var val:Boolean = !bg.visible;
			bg.visible = val;
			toggleBgBtn.filters = val ? [new GlowFilter(0xbbbbbb, 1, 4, 4, 2, 1, true)] : null;
		}
	]]></fx:Script>

	<s:VGroup width="100%" height="100%" verticalAlign="middle" horizontalAlign="center" paddingBottom="5">
		<s:Group id="containerGrp" width="100%" height="100%">
			<s:SpriteVisualElement visible="false" id="bg"/>
			<s:SpriteVisualElement id="container"/>
		</s:Group>
		<s:HGroup width="100%" horizontalAlign="center">
			<s:Button click="switchFrame(false)" label="prev"/>
			<s:Button click="togglePlay()" id="playBtn" label="stop"/>
			<s:Button click="switchFrame()" label="next"/>
			<s:Button click="toggleBg()" id="toggleBgBtn" label="background"/>
		</s:HGroup>
		<s:Label id="frameLbl"/>
	</s:VGroup>

</s:TitleWindow>
